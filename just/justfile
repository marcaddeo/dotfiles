import "~/dotfiles/just/common.just"
import? "/Users/maddeo/dotfiles/just/work.just"

mod dotfiles "~/dotfiles/just/dotfiles.just"
mod vrt "~/dotfiles/just/vrt.just"

init_justfile := """
# Display this list of available commands
@list:
    just --justfile "{{ source_file() }}" --list
"""

# Display this list of available commands
@list:
    just --justfile "{{ source_file() }}" --list

# Edit the global justfile
@edit:
    $EDITOR "{{ justfile() }}"

# Initialize a justfile with my custom default template
@init name="justfile":
    [[ -e "{{ name }}" ]] || echo '{{ init_justfile }}' > "{{ name }}"

# Format just files in ~/dotfiles/just/
@format:
    for file in $(find '{{ dotfiles / "just" }}' -type f); do \
        just --unstable --fmt --justfile=$file; \
    done
