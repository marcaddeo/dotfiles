import "~/dotfiles/just/common.just"
import? "/Users/maddeo/dotfiles/just/work.just"

mod dotfiles "~/dotfiles/just/dotfiles.just"
mod vrt "~/dotfiles/just/vrt.just"

init_justfile := """
chooser := "grep -v choose | fzf --tmux"

# Display this list of available commands
@list:
    just --justfile "{{ source_file() }}" --list

alias c := choose
# Open an interactive chooser of available commands
[no-exit-message]
@choose:
    just --justfile "{{ source_file() }}" --chooser "{{ chooser }}" --choose 2>/dev/null

alias e := edit
# Edit the justfile
@edit:
    $EDITOR "{{ justfile() }}"
"""

# Display this list of available commands
@list:
    just --justfile "{{ source_file() }}" --list

alias c := choose
# Open an interactive chooser of available commands
[no-exit-message]
@choose:
    just --justfile "{{ source_file() }}" --chooser "{{ chooser }}" --choose 2>/dev/null

alias e := edit
# Edit the global justfile
@edit:
    cd "{{ dotfiles }}/just" && $EDITOR "{{ justfile() }}"

# Initialize a justfile with my custom default template
@init name="justfile":
    [[ -e "{{ name }}" ]] || echo '{{ init_justfile }}' > "{{ name }}"

# Format just files in ~/dotfiles/just/
@format:
    for file in $(find '{{ dotfiles / "just" }}' -type f); do \
        just --unstable --fmt --justfile=$file; \
    done
